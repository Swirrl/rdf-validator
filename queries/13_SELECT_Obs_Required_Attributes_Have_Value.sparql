PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT (?attr AS ?requiredAttributeWithoutValue) ?obs WHERE {
    
   {
    SELECT ?cp {
      {
        BIND(qb:componentProperty as ?cp)
      }
      UNION
      {   SELECT ?cp 
        WHERE { 
          ?cp rdfs:subPropertyOf+ qb:componentProperty . }  
      }
    }
  }

    ?obs qb:dataSet/qb:structure/qb:component ?component .
    ?component qb:componentRequired "true"^^xsd:boolean ;
               ?cp ?attr .
    FILTER NOT EXISTS { ?obs ?attr [] }
}

#! IC-13. Required attributes - Every qb:Observation has a value for each declared attribute that is marked as required.

# Note - Querying within data model for properties that are inherited from qb:componentProperty
#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-13           ####
# 
# ASK {
#     ?obs qb:dataSet/qb:structure/qb:component ?component .
#     ?component qb:componentRequired "true"^^xsd:boolean ;
#                qb:componentProperty ?attr .
#     FILTER NOT EXISTS { ?obs ?attr [] }
# }
# 
#######################################################################