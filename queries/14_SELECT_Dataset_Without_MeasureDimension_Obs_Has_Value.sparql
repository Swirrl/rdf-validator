PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT (?obs AS ?obsMissingValueForMeasure) ?measure  
WHERE {

	{
	  	SELECT ?cp {
	  	{
	  		BIND(qb:componentProperty as ?cp)
	  	}
	  	UNION
	  	{ 	SELECT ?cp 
	  		WHERE { 
	  			?cp rdfs:subPropertyOf+ qb:componentProperty . }  
			}
	  	}
	}

    # Observation in a non-measureType cube
    ?obs qb:dataSet/qb:structure ?dsd .
    FILTER NOT EXISTS { 
    	?dsd qb:component ?component .
    	?component ?cp qb:measureType 
	}

    # verify every measure is present
    ?dsd qb:component ?component . 
    ?component ?cp ?measure .
    ?measure a qb:MeasureProperty;
    FILTER NOT EXISTS { ?obs ?measure [] }
}

#! IC-14. All measures present - In a qb:DataSet which does not use a Measure dimension then each individual qb:Observation must have a value for every declared measure. 

#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-14           ####
# 
# ASK {
#     # Observation in a non-measureType cube
#     ?obs qb:dataSet/qb:structure ?dsd .
#     FILTER NOT EXISTS { ?dsd qb:component/qb:componentProperty qb:measureType }
# 
#     # verify every measure is present
#     ?dsd qb:component/qb:componentProperty ?measure .
#     ?measure a qb:MeasureProperty;
#     FILTER NOT EXISTS { ?obs ?measure [] }
# }
# 
#######################################################################