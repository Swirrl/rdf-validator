#IC-5. Concept dimensions have code lists - Every dimension with range skos:Concept must have a qb:codeList. 
# Note - Querying for subClassOf skos:Concept within data model

PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT (?dim AS ?dimensionWithNoCodeList) ?skosConcept
WHERE {
  { SELECT ?skosConcept WHERE { ?skosConcept rdfs:subClassOf skos:Concept } }
  ?dim a qb:DimensionProperty ;
       rdfs:range ?skosConcept .
  FILTER NOT EXISTS { ?dim qb:codeList [] }
}


#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-5            ####
# 
# ASK {
#   ?dim a qb:DimensionProperty ;
#        rdfs:range skos:Concept .
#   FILTER NOT EXISTS { ?dim qb:codeList [] }
# }
# 
#######################################################################