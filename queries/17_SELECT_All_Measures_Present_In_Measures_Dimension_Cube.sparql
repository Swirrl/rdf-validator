PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?dsd WHERE {
  {
    # Count number of other measures found at each point 
    SELECT ?dsd ?numMeasures (COUNT(?obs2) AS ?count) WHERE {      
      {
        # Find the DSDs and check how many measures they have
        SELECT ?dsd (COUNT(?m) AS ?numMeasures) WHERE {
          {
            ?dsd qb:component ?comp .
            ?comp ?cp ?m .
            ?m a qb:MeasureProperty .
            ?cp rdfs:subPropertyOf* qb:componentProperty .
          } UNION {
            ?dsd qb:component/qb:measure ?m .
          }                  
        } GROUP BY ?dsd
      }
        
      # Observation in measureType cube
      ?obs1 qb:dataSet/qb:structure ?dsd;
            qb:dataSet ?dataset ;
            qb:measureType ?m1 .
    
      # Other observation at same dimension value
      ?obs2 qb:dataSet ?dataset ;
            qb:measureType ?m2 .
      FILTER NOT EXISTS { 
        {
          ?dsd qb:component ?comp .
          ?comp ?cp ?dim .
          ?cp rdfs:subPropertyOf* qb:componentProperty .
          ?dim a qb:DimensionProperty .
        } UNION {
          ?dsd qb:component/qb:dimension ?dim .
        }
              
        FILTER (?dim != qb:measureType)              
        ?obs1 ?dim ?v1 . 
        ?obs2 ?dim ?v2. 
        FILTER (?v1 != ?v2)
      }
          
    } GROUP BY ?dsd ?obs1 ?numMeasures
      HAVING (?count != ?numMeasures)
  }
}    

#! IC-17. All measures present in measures dimension cube -  In a qb:DataSet which uses a Measure dimension then if there is a Observation for some combination of non-measure dimensions then there must be other Observations with the same non-measure dimension values for each of the declared measures. 

#TODO
#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-17           ####
# 
# ASK {
#   {
#       # Count number of other measures found at each point 
#       SELECT ?numMeasures (COUNT(?obs2) AS ?count) WHERE {
#           {
#               # Find the DSDs and check how many measures they have
#               SELECT ?dsd (COUNT(?m) AS ?numMeasures) WHERE {
#                   ?dsd qb:component/qb:componentProperty ?m.
#                   ?m a qb:MeasureProperty .
#               } GROUP BY ?dsd
#           }
#         
#           # Observation in measureType cube
#           ?obs1 qb:dataSet/qb:structure ?dsd;
#                 qb:dataSet ?dataset ;
#                 qb:measureType ?m1 .
#     
#           # Other observation at same dimension value
#           ?obs2 qb:dataSet ?dataset ;
#                 qb:measureType ?m2 .
#           FILTER NOT EXISTS { 
#               ?dsd qb:component/qb:componentProperty ?dim .
#               FILTER (?dim != qb:measureType)
#               ?dim a qb:DimensionProperty .
#               ?obs1 ?dim ?v1 . 
#               ?obs2 ?dim ?v2. 
#               FILTER (?v1 != ?v2)
#           }
#           
#       } GROUP BY ?obs1 ?numMeasures
#         HAVING (?count != ?numMeasures)
#   }
# }
# 
#######################################################################