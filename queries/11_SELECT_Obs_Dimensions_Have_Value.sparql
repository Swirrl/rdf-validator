PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT (?obs AS ?obsWithMissingValuesOnDimension) 
WHERE {

	{
	  	SELECT ?cp {
	  	{
	  		BIND(qb:componentProperty as ?cp)
	  	}
	  	UNION
	  	{ 	SELECT ?cp 
	  		WHERE { 
	  			?cp rdfs:subPropertyOf+ qb:componentProperty . }  
			}
	  	}
  	}

    ?obs qb:dataSet/qb:structure/qb:component ?component .
    ?component ?cp ?dim  .
    ?dim a qb:DimensionProperty;
    FILTER NOT EXISTS { ?obs ?dim [] }
}

#! IC-11. All dimensions required - Every qb:Observation has a value for each dimension declared in its associated qb:DataStructureDefinition.

#. Note - split property path so that variable can be used to include subProperties of qb:componentProperty
#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-11           ####
# 
# ASK {
#     ?obs qb:dataSet/qb:structure/qb:component/qb:componentProperty ?dim .
#     ?dim a qb:DimensionProperty;
#     FILTER NOT EXISTS { ?obs ?dim [] }
# }
# 
#######################################################################