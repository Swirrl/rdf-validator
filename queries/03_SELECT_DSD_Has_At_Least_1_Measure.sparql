PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT ?dsd WHERE {
  ?dsd a qb:DataStructureDefinition .
  FILTER NOT EXISTS {
    # ok if dsd has component with qb:measure
    ?dsd qb:component [qb:measure ?m] .
  }
  FILTER NOT EXISTS {
    # ok if component has qb:componentProperty (or a subProperty)
    # of MeasureType
    ?dsd qb:component ?comp .
    ?comp ?cp ?p .
    ?cp rdfs:subPropertyOf* qb:componentProperty .
    ?p a qb:MeasureType .
  }
}

#! IC-3. DSD includes measure - Every qb:DataStructureDefinition must include at least one declared measure. 

# Note - this includes a reasoning workaround by looking for subProperties of qb:componentProperty. 
#######################################################################
####                ORIGINAL ASK QUERY FROM SPEC  IC-3            ####
# 
# ASK {
#  ?dsd a qb:DataStructureDefinition .
#  FILTER NOT EXISTS { ?dsd qb:component [qb:componentProperty [a qb:MeasureProperty]] }
# }
# 
#######################################################################