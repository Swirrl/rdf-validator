PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT REDUCED (?component AS ?componentWrongTypeToBeOptional) 
WHERE {

 {
  	SELECT ?cp {
  	{
  		BIND(qb:componentProperty as ?cp)
  	}
  	UNION
  	{ 	SELECT ?cp 
  		WHERE { 
  			?cp rdfs:subPropertyOf+ qb:componentProperty . }  
		}
  	}
  }

  ?dsd qb:component ?componentSpec .
  ?componentSpec qb:componentRequired "false"^^xsd:boolean ;
                 ?cp ?component .
  FILTER NOT EXISTS { ?component a qb:AttributeProperty }
}

#! IC-6. Only attributes may be optional - The only components of a qb:DataStructureDefinition that may be marked as optional, using qb:componentRequired are attributes. 

# Note - this includes a reasoning workaround by looking for subProperties of qb:componentProperty. 
#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-6            ####
# 
# ASK {
#   ?dsd qb:component ?componentSpec .
#   ?componentSpec qb:componentRequired "false"^^xsd:boolean ;
#                  qb:componentProperty ?component .
#   FILTER NOT EXISTS { ?component a qb:AttributeProperty }
# }
# 
#######################################################################