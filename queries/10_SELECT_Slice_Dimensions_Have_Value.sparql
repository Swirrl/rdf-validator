PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX qb:      <http://purl.org/linked-data/cube#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>

SELECT (?slice AS ?sliceWithMissingValuesOnDimension) ?dimension  
WHERE {

    {
	  	SELECT ?cp {
	  	{
	  		BIND(qb:componentProperty as ?cp)
	  	}
	  	UNION
	  	{ 	SELECT ?cp 
	  		WHERE { 
	  			?cp rdfs:subPropertyOf+ qb:componentProperty . }  
			}
	  	}
	  }

    ?slice qb:sliceStructure [?cp ?dimension] .
    FILTER NOT EXISTS { ?slice ?dimension [] }
}

#! IC-10. Slice dimensions complete - Every qb:Slice must have a value for every dimension declared in its qb:sliceStructure.

# Note - Querying within data model for properties that are types inherited from qb:componentProperty

#######################################################################
####                 ORIGINAL ASK QUERY FROM SPEC  IC-10           ####
# 
# ASK {
#   ?slice qb:sliceStructure [qb:componentProperty ?dim] .
#   FILTER NOT EXISTS { ?slice ?dim [] }
# }
# 
#######################################################################